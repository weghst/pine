buildscript {
    repositories {
        mavenLocal()
        maven { url gradle.privateMavenRepositoryUrl }
        mavenCentral()
    }

    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE"
    }
}

configure(allprojects) { project ->

    group = "com.weghst.pine"
    version = "1.0-SNAPSHOT"

    ext.slf4jVersion = "1.7.12"
    ext.springVersion = "4.2.0.RELEASE"
    ext.freemarkerVersion = "2.3.23"
    ext.jacksonVersion = "2.6.0"
    ext.logbackVersion = "1.1.3"
    ext.bonecpVersion = "0.8.0.RELEASE"
    ext.resteasyVersion = "3.0.12.Final"
    ext.mariadbVersion = "1.2.0"
    ext.testngVersion = "6.9.6"

    apply plugin: "java"
    apply plugin: "findbugs"
    apply plugin: "checkstyle"
    apply plugin: "pmd"

    apply plugin: "maven"
    apply plugin: "idea"
    apply plugin: "eclipse"

    repositories {
        mavenLocal()
        maven { url gradle.privateMavenRepositoryUrl }
        mavenCentral()
    }

    configurations {
        deployerJars
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: "file://localhost/tmp-pine/") {
                Properties authProps = new Properties()
                authProps.load(new FileInputStream("/maven-upload-auth.properties"))
                authentication(userName: authProps["username"], password: authProps["password"])
            }
        }
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    }

}

configure(subprojects) { project ->

    configurations {
        springLoaded

        compile.exclude group: "commons-logging", module: "commons-logging"
    }

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    jar {
        baseName = "$rootProject.name-$project.name"
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$slf4jVersion")

        testCompile("org.testng:testng:$testngVersion") {
            exclude module: "guice"
            exclude module: "junit"
            exclude module: "ant"
        }

        springLoaded "org.springframework:springloaded:1.2.4.RELEASE"
    }
}

project("api") {

    dependencies {
        compile("commons-codec:commons-codec:1.10")
        compile("com.google.guava:guava:19.0-rc1")
        compile("com.google.code.findbugs:jsr305:3.0.0")
        compile("org.glassfish:javax.json:1.0.4")
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr353:$jacksonVersion")
    }
}

project("core") {

    jar {
        manifest {
            attributes(
                    "Liquibase-Package": "com.weghst.pine.liquibase"
            )
        }
    }

    dependencies {
        compile(project(":api"))
        compile("org.apache.commons:commons-lang3:3.4")
        compile("javax.mail:mail:1.4.7")
        compile("org.freemarker:freemarker:$freemarkerVersion")
        compile("org.springframework:spring-jdbc:$springVersion")
        compile("org.springframework:spring-context:$springVersion")
        compile("org.springframework:spring-context-support:$springVersion")
        compile("org.quartz-scheduler:quartz:2.2.1") {
            exclude module: "c3p0"
        }
        compile("org.quartz-scheduler:quartz-jobs:2.2.1")
        compile("redis.clients:jedis:2.7.3")
        compile("org.aspectj:aspectjweaver:1.8.6")
        compile("org.liquibase:liquibase-core:3.4.1")

        testCompile("org.springframework:spring-test:$springVersion")
        testCompile("com.jolbox:bonecp-spring:$bonecpVersion")
        testCompile("org.mariadb.jdbc:mariadb-java-client:$mariadbVersion") {
            exclude module: "commons-dbcp"
        }
    }
}

project("auth") {

}

project("web") {

    apply plugin: "war"
    apply plugin: "org.akhikhl.gretty"

    gretty {
        def slPath = "-javaagent:" + configurations.springLoaded.asPath;
        jvmArgs = [slPath, "-noverify"]

        servletContainer = "tomcat7"
        recompileOnSourceChange = false
        reloadOnClassChange = false
        // reloadOnConfigChange = false
        // reloadOnLibChange = false

        afterEvaluate {
            appRunDebug {
                debugPort = 5005     // default
                debugSuspend = true // default
            }
        }
        port = 8080
        contextPath = "/"
    }

    dependencies {
        providedCompile("javax.servlet:javax.servlet-api:3.1.0")

        compile(project(":core"));
        compile("org.ow2.asm:asm:5.0.4")
        compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")
        compile("org.springframework:spring-webmvc:$springVersion")

        compile("org.jboss.resteasy:resteasy-jackson2-provider:$resteasyVersion")
        compile("org.jboss.resteasy:resteasy-spring:$resteasyVersion") {
            exclude group: "org.jboss.resteasy", module: "resteasy-jettison-provider"
        }

        compile("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
        compile("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
        compile("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")

        runtime("org.mariadb.jdbc:mariadb-java-client:$mariadbVersion")
        runtime("com.jolbox:bonecp-spring:$bonecpVersion")
        runtime("ch.qos.logback:logback-classic:$logbackVersion")

//        compile("org.springframework.boot:spring-boot-legacy:1.0.1.RELEASE")
//        providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:1.2.6.RELEASE")
//        compile("org.springframework.boot:spring-boot-starter-web")
    }

}
