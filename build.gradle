buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "com.bmuschko:gradle-tomcat-plugin:2.2.2"
    }
}

configure(allprojects) { project ->

    group = "com.weghst.pine"
    version = "1.0-SNAPSHOT"

    ext.slf4jVersion = "1.7.12"
    ext.springVersion = "4.2.0.RELEASE"
    ext.freemarkerVersion = "2.3.23"
    ext.jacksonVersion = "2.6.0"
    ext.logbackVersion = "1.1.3"
    ext.bonecpVersion = "0.8.0.RELEASE"
    ext.resteasyVersion = "3.0.12.Final"
    ext.mariadbVersion = "1.2.0"
    ext.testngVersion = "6.9.6"

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "idea"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$slf4jVersion")

        testCompile("org.testng:testng:$testngVersion") {
            exclude module: "guice"
            exclude module: "junit"
            exclude module: "ant"
        }
    }
}

project("api") {

    dependencies {
        compile("commons-codec:commons-codec:1.10")
        compile("com.google.guava:guava:19.0-rc1")
        compile("com.google.code.findbugs:jsr305:3.0.0")
        compile("org.glassfish:javax.json:1.0.4")
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr353:$jacksonVersion")
    }
}

project("core") {

    dependencies {
        compile(project(":api"))
        compile("org.apache.commons:commons-lang3:3.4")
        compile("javax.mail:mail:1.4.7")
        compile("org.freemarker:freemarker:$freemarkerVersion")
        compile("org.springframework:spring-jdbc:$springVersion")
        compile("org.springframework:spring-context:$springVersion")
        compile("org.springframework:spring-context-support:$springVersion")
        compile("org.quartz-scheduler:quartz:2.2.1") {
            exclude module: "c3p0"
        }
        compile("org.quartz-scheduler:quartz-jobs:2.2.1")
        compile("redis.clients:jedis:2.7.3")
        compile("org.aspectj:aspectjweaver:1.8.6")
        compile("org.liquibase:liquibase-core:3.4.1")

        testCompile("org.springframework:spring-test:$springVersion")
        testCompile("com.jolbox:bonecp-spring:$bonecpVersion")
        testCompile("org.mariadb.jdbc:mariadb-java-client:$mariadbVersion") {
            exclude module: "commons-dbcp"
        }
    }
}

project("web") {

    apply plugin: "war"
    apply plugin: "com.bmuschko.tomcat"

    dependencies {
        providedCompile("javax.servlet:javax.servlet-api:3.1.0")

        compile(project(":core"));
        compile("org.ow2.asm:asm:5.0.4")
        compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")
        compile("org.springframework:spring-webmvc:$springVersion")

        compile("org.jboss.resteasy:resteasy-jackson2-provider:$resteasyVersion")
        compile("org.jboss.resteasy:resteasy-spring:$resteasyVersion")

        compile("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
        compile("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
        compile("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")

        runtime("org.mariadb.jdbc:mariadb-java-client:$mariadbVersion")
        runtime("com.jolbox:bonecp-spring:$bonecpVersion")
        runtime("ch.qos.logback:logback-classic:$logbackVersion")

    }

    tomcat {
        contextPath = "/"

        dependencies {
            def tomcatVersion = '7.0.59'
            tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
                    "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
                    "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
        }
    }
}
